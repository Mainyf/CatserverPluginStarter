import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar
import io.github.mainyf.remapjar.RemapJar
import io.github.mainyf.remapjar.RemapJarPlugin

plugins {
    id "java"
    id "org.jetbrains.kotlin.jvm" version "1.3.61"
    id "com.github.johnrengelman.shadow" version "4.0.4"
    id 'net.minecrell.plugin-yml.bukkit' version '0.3.0'
}

repositories {
    mavenLocal()
    maven {
        url = uri("https://maven.aliyun.com/repository/public")
    }

    maven {
        name = "spigotmc-public"
        url = "https://hub.spigotmc.org/nexus/content/groups/public/"
    }
}

configurations {
    embed
    getByName("compile").extendsFrom(embed)
}

dependencies {
    compile(project.fileTree("lib") {
        include "**/*.jar"
    })
    embed("org.jetbrains.kotlin:kotlin-stdlib-jdk8")
    embed("org.jetbrains.kotlin:kotlin-reflect")
}

bukkit {
    name = project.name
    main = project.mainClass
    version = project.version
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

apply plugin: RemapJarPlugin
task obfPlugin(type: RemapJar) {
    group = "bukkit"
    dependsOn(jar)
    inputJar = jar.outputs.files.singleFile
}

task bundlePlugin(type: ShadowJar) {
    dependsOn(obfPlugin)
    group = "bukkit"
    classifier = "shade"
    baseName = project.name
    from(obfPlugin)
    configurations = [project.configurations.embed]
}